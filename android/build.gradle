group = "com.deeplinkly.flutter_deeplinkly"
version = "1.0-SNAPSHOT"

buildscript {
    ext.kotlin_version = "2.2.0"
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.7.0")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

android {
    namespace = "com.deeplinkly.flutter_deeplinkly"

    compileSdk = 35

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    defaultConfig {
        minSdk = 21
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }
}

dependencies {
    testImplementation("org.jetbrains.kotlin:kotlin-test")
    testImplementation("org.mockito:mockito-core:5.17.0")
    implementation 'com.android.installreferrer:installreferrer:2.2'
    implementation 'com.google.android.gms:play-services-ads-identifier:18.2.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.1"
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:2.2.0'
    implementation "androidx.work:work-runtime-ktx:2.10.3"
    implementation "androidx.core:core-ktx:1.12.0"
    
    // Flutter embedding dependencies
    // Flutter automatically provides these when building through 'flutter run'
    // For direct builds, we try to find the Flutter JAR
    def flutterJar = "$flutterRoot/bin/cache/artifacts/engine/android-arm/flutter.jar"
    if (file(flutterJar).exists()) {
        compileOnly files(flutterJar)
    } else {
        // Try alternative location
        def altFlutterJar = "$flutterRoot/bin/cache/artifacts/engine/android/flutter.jar"
        if (file(altFlutterJar).exists()) {
            compileOnly files(altFlutterJar)
        }
    }
}
